package zoot.analyse ;

import java.util.*;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.tds.*;
import zoot.arbre.instructions.* ;
import zoot.exceptions.AnalyseSyntaxiqueException;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;

        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)
                    m.append(" colonne : " + (s.right+1)) ;
            }

            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* Terminals (tokens returned by the scanner).  */

terminal Symbol    VARIABLES, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGALE, FONCTIONS, PARENTHESE, RETOURNE;
terminal String    CSTENTIERE, IDF, TYPE, CSTBOOLEEN ;

/* Non terminals used in the grammar section.  */
non terminal LDCL, DCL, LFONC, FONC, PROGFONCT, RET ;
non terminal ArbreAbstrait PROG, LINST ;
non terminal Instruction INST ;
non terminal Expression EXP ;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG    ::=     VARIABLES LDCL FONCTIONS LFONC DEBUT LINST:li FIN
                {: RESULT = li ; :}
                ;

PROGFONCT    ::=  VARIABLES LDCL DEBUT LINST:li RET FIN
                {: RESULT = li ; :}
                ;


RET     ::=    RETOURNE IDF:idf POINTVIRGULE
                {:
                :};



LINST   ::=     LINST:li  INST:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                :}
        |
                INST:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
                |
                IDF:idf EGALE EXP:e  POINTVIRGULE
                {: RESULT = new Affectation(idf,e, eleft + 1); :}
                ;

LFONC  ::=      LFONC FONC | FONC;

FONC   ::=      TYPE:t IDF:idf PARENTHESE PROGFONCT {:
                    TDS.getInstance().ajouter(new EntreeFonction(idf, idfleft+1), new SymboleFonction(t));
                :}
                ;

LDCL   ::=      LDCL DCL | DCL;



DCL    ::=     TYPE:t IDF:idf POINTVIRGULE
                {:
                    TDS.getInstance().ajouter(new EntreeVariable(idf, idfleft+1), new SymboleVariable(t));
                :}
                ;

EXP    ::=     CSTENTIERE:c
               {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
               |
               IDF:i
               {: RESULT = new Idf(i, ileft + 1); :}
               |
               IDF:i PARENTHESE
               {: RESULT = new AppelFonction(ileft+1,i) ; :}               |
               CSTBOOLEEN:b
               {: RESULT = new ConstanteBooleenne(b, bleft + 1) ; :}
                ;